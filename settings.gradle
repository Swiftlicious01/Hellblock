rootProject.name = "Hellblock"

// Always include core + api
include(":core", ":api")

// Auto-discover all version modules under paper/ and spigot/
// Only include valid version folders under paper/ and spigot/
["paper", "spigot"].each { parent ->
    def parentDir = file(parent)
    if (parentDir.exists()) {
        parentDir.listFiles()
            ?.findAll { it.isDirectory() && !it.name.startsWith(".") } // Ignore .settings, .git, etc.
            ?.each { dir ->
                def path = ":${parent}:${dir.name}"
                include(path)
                project(path).projectDir = dir
            }
    }
}


// Dependency resolution & version catalogs (TOML-based)
dependencyResolutionManagement {
    repositories {
        mavenCentral()
        gradlePluginPortal()
        maven { url = 'https://repo.papermc.io/repository/maven-public' }
        maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    }

    versionCatalogs {
        hellblockLibs {
            from(files("gradle/libs.versions.toml"))
        }
    }
}