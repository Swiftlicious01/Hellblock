// --- ensure Shadow uses its own ASM version, not Paperweightâ€™s ---
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.ow2.asm:asm:9.7'
        classpath 'org.ow2.asm:asm-commons:9.7'
        classpath 'org.ow2.asm:asm-tree:9.7'
        classpath 'org.ow2.asm:asm-util:9.7'
    }
}

// --- now declare plugins (Gradle 9+ requires this order) ---
plugins {
    id 'com.gradleup.shadow' version '9.2.2'
    id 'java'
}

description = "Core for Hellblock"

repositories {
    maven {
        url = 'https://maven.enginehub.org/repo/'
        content {
            includeGroup 'com.sk89q.worldedit'
            includeGroup 'com.sk89q.worldguard'
            includeGroup 'com.sk89q.worldedit.worldedit-libs'
            includeGroup 'com.sk89q.worldguard.worldguard-libs'
        }
    }
    maven { url = 'https://jitpack.io/' }
    maven { url = 'https://libraries.minecraft.net' }
    maven {
        url = 'https://repo.papermc.io/repository/maven-public'
        content { includeGroup 'dev.folia' }
        metadataSources { mavenPom(); artifact() }
    }
    maven { 
    	url = 'https://repo.codemc.io/repository/maven-public/' 
    	metadataSources { mavenPom(); artifact() }
    }
    maven { url = 'https://mvn.lumine.io/repository/maven-public/' }
    maven { url = 'https://repo.rosewooddev.io/repository/public/' }
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://repo.bsdevelopment.org/releases' }
    maven { url = 'https://repo.glaremasters.me/repository/concuncan/' }
    maven { url = 'https://repo.bg-software.com/repository/api/' }
    maven { url = 'https://nexus.neetgames.com/repository/maven-public' }
    maven { url = 'https://repo.infernalsuite.com/repository/maven-snapshots/' }
    maven { url = 'https://repo.rapture.pw/repository/maven-releases/' }
    maven { url = 'https://repo.opencollab.dev/main/' }
    maven { url = 'https://repo.minebench.de/' }
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
        content { includeGroup 'me.clip' }
    }
    maven { url = 'https://repo.onarandombox.com/content/groups/public' }
}

dependencies {
    // Paper APIs (or spigot for testing purposes only, will always depend on paper)
    compileOnly hellblockLibs.folia
    // compileOnly hellblockLibs.spigot
    // compileOnly hellblockLibs.paper
    compileOnly hellblockLibs.aswm

    // Internal API
    implementation project(":api")

    // --- ASM FIX for Shadow 9 ---
    compileOnly 'org.ow2.asm:asm:9.7'
    compileOnly 'org.ow2.asm:asm-commons:9.7'
    compileOnly 'org.ow2.asm:asm-tree:9.7'
    compileOnly 'org.ow2.asm:asm-util:9.7'

	// --- Auto-discover and add versioned Paper & Spigot modules safely ---
    	
	// PAPER & SPIGOT IMPLS â€” include jars in runtime bundle
	rootProject.childProjects['paper']?.childProjects
    	?.findAll { it.key ==~ /v\d+_\d+_R\d+/ }
    	?.each { name, proj ->
        	compileOnly project(proj.path)
    	}

	rootProject.childProjects['spigot']?.childProjects
    	?.findAll { it.key ==~ /v\d+_\d+_R\d+/ }
    	?.each { name, proj ->
        	compileOnly project(proj.path)
    	}

    implementation hellblockLibs.rtagcore
    implementation(hellblockLibs.rtagitem) { transitive = false }
    implementation(hellblockLibs.rtagentity) { transitive = false }
    implementation(hellblockLibs.rtagblock) { transitive = false }

    compileOnly(hellblockLibs.adventureapi) {
        exclude module: 'adventure-bom'
        exclude module: 'checker-qual'
        exclude module: 'annotations'
    }
    compileOnly hellblockLibs.adventureminimessage
    compileOnly hellblockLibs.adventureplatformbukkit
    compileOnly hellblockLibs.adventurejsonlegacy
    compileOnly hellblockLibs.adventurenbt
    compileOnly(hellblockLibs.adventuregson) {
        exclude group: 'com.google.code.gson'
    }

    implementation hellblockLibs.paperlib
    implementation(hellblockLibs.authlib) {
    	exclude group: 'commons-io', module: 'commons-io'
    	exclude group: 'com.google.code.findbugs', module: 'jsr305'
	}
    compileOnly hellblockLibs.bytebuddy
    compileOnly hellblockLibs.spotbugs
    compileOnly hellblockLibs.jackson
    compileOnly hellblockLibs.jacksonannotations

    // Cloud & optional APIs
    compileOnly hellblockLibs.cloudcore
    compileOnly hellblockLibs.cloudminecraftextras
    compileOnly hellblockLibs.cloudpaper
    compileOnly hellblockLibs.immutables
    compileOnly hellblockLibs.placeholderapi
    compileOnly hellblockLibs.gson
    compileOnly hellblockLibs.log4j
    compileOnly hellblockLibs.slf4j

    // WorldEdit / WorldGuard
    compileOnly hellblockLibs.worldeditcore
    compileOnly hellblockLibs.worldeditbukkit
    compileOnly hellblockLibs.worldeditlibscore
    compileOnly hellblockLibs.worldeditlibsbukkit
    compileOnly hellblockLibs.worldguardcore
    compileOnly hellblockLibs.worldguardbukkit
    compileOnly hellblockLibs.worldguardlibscore

    // Database & caching
    compileOnly hellblockLibs.sqlite
    compileOnly hellblockLibs.h2
    compileOnly hellblockLibs.mongodb
    compileOnly hellblockLibs.hikari
    compileOnly hellblockLibs.jedis
    compileOnly hellblockLibs.caffeine
    compileOnly hellblockLibs.exp4j
    compileOnly hellblockLibs.lz4
    compileOnly hellblockLibs.boostedyaml

    // Geyser
    compileOnly hellblockLibs.geyser
    compileOnly hellblockLibs.floodgate

    // External plugin APIs
    compileOnly(hellblockLibs.vault) { exclude group: 'org.bukkit' }
    compileOnly hellblockLibs.multiversecore
    compileOnly hellblockLibs.mythicmobs
    compileOnly hellblockLibs.playerpoints
    compileOnly(hellblockLibs.shopgui) { exclude group: 'org.spigotmc', module: 'spigot-api' }
    compileOnly hellblockLibs.jobs
    compileOnly hellblockLibs.wildstacker
    compileOnly hellblockLibs.mcmmo
    compileOnly hellblockLibs.stackermob
    compileOnly hellblockLibs.simplepets
    compileOnly hellblockLibs.tradeshop
    compileOnly hellblockLibs.chestshop
    compileOnly hellblockLibs.quickshop
    compileOnly hellblockLibs.openinv
    compileOnly hellblockLibs.jsonconfiguration
    compileOnly hellblockLibs.jsonsmart
 
    // Local libs
    compileOnly files("libs/AdvancedEnchantments-api.jar")
}

tasks.named("processResources") {
    // --- Find all versioned Paper/Spigot subprojects ---
    def versionedProjects = rootProject.subprojects.findAll {
        it.path.startsWith(":paper:") || it.path.startsWith(":spigot:")
    }.findAll { it.name ==~ /v\d+_\d+_R\d+/ }

    dependsOn versionedProjects.collect { it.tasks.named("jar") }

    // --- Copy each JAR into runtime folder with proper prefix ---
    versionedProjects.each { proj ->
        def jarFile = proj.layout.buildDirectory.file("libs/${proj.name}.jar").get().asFile
        def prefix = proj.path.startsWith(":paper:") ? "paper-" : "spigot-"

        from(jarFile) {
            into "runtime"
            rename { "${prefix}${proj.name}.jar" }
        }
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    doFirst {
        logger.lifecycle("Including versioned runtime jars: ${versionedProjects*.path}")
    }

	doLast {
        fileTree("$buildDir/resources/main/runtime").matching { include '**/*.jar' }.each { f ->
            def gzFile = new File(f.parentFile, f.name + ".gz")
        	new java.util.zip.GZIPOutputStream(new FileOutputStream(gzFile)).withCloseable { out ->
            	f.withInputStream { it.transferTo(out) }
        	}
            f.delete()
        }
        println "âœ… Compressed runtime jars to GZIP."

    	// --- Gzip non-plugin.yml YAML files as well ---
    	def compressedCount = 0
    	fileTree("${buildDir}/resources/main").matching { include '**/*.yml' }.files.each { file ->
        	if (file.name == 'plugin.yml') return // Skip plugin.yml
        	def gzipFile = new File(file.parentFile, file.name + ".gz")
        	new java.util.zip.GZIPOutputStream(new FileOutputStream(gzipFile)).withCloseable { out ->
            	file.withInputStream { it.transferTo(out) }
        	}
        	file.delete()
        	compressedCount++
    	}
    	if (compressedCount > 0) {
        	println "ðŸª¶ Gzipped ${compressedCount} YAML resource(s)."
    	}
	}
}

compileJava {
    options.compilerArgs += ['-Xlint:all', '-Xlint:-processing']
    options.encoding = 'UTF-8'
}

java {
    toolchain { languageVersion = JavaLanguageVersion.of(21) }
}

sourceSets.all {
    allSource.exclude("**/.DS_Store")
    resources.exclude("**/.DS_Store")
}

configurations.all {
    resolutionStrategy.force 'com.google.guava:guava-mini:33.3.1-jre'
    resolutionStrategy.force 'org.ow2.asm:asm:9.7'
    resolutionStrategy.force 'org.ow2.asm:asm-commons:9.7'
    resolutionStrategy.force 'org.ow2.asm:asm-tree:9.7'
    resolutionStrategy.force 'org.ow2.asm:asm-util:9.7'
    exclude group: 'org.apache.maven.plugins', module: 'maven-javadoc-plugin'
    exclude group: 'org.checkerframework', module: 'checker-qual'
    exclude group: 'org.apache.commons', module: 'commons-lang3'
    exclude group: 'org.apache.commons', module: 'commons-io'
}

tasks.named("assemble") {
    dependsOn rootProject.subprojects.findAll { it.name ==~ /v\d+_\d+_R\d+/ }.collect { it.tasks.named("jar") }
}

tasks.register('analyzeShadowJars') {
    dependsOn tasks.shadowJar
    doLast {
        def jarFile = file("${rootDir}/target/Hellblock-${hellblockLibs.versions.pluginVersion.get()}.jar")
        println "\nðŸ” Analyzing JAR: ${jarFile.name} (${jarFile.length() / 1024 / 1024} MB)\n"
        
        ant.unzip(src: jarFile, dest: "$buildDir/tmp/analyze")
        def dir = file("$buildDir/tmp/analyze")
        def files = []
        dir.eachFileRecurse { f ->
            if (f.isFile()) files << [file: f, size: f.length()]
        }

        files.sort { -it.size }.take(30).each { entry ->
            println String.format("%6.2f MB  %s", entry.size / 1024 / 1024, entry.file.path - dir.path)
        }
        println "\nTotal: ${(files.sum { it.size } / 1024 / 1024).round(2)} MB"
    }
}

tasks.withType(Jar).configureEach {
    exclude("**/.DS_Store", "**/*.kotlin_metadata", "**/*.kotlin_module",
        "META-INF/LICENSE*", "META-INF/NOTICE*", "META-INF/*.RSA",
        "META-INF/*.SF", "META-INF/*.DSA", "META-INF/versions/**",
        "**/module-info.class")
}

tasks.named("shadowJar") { task ->
    dependsOn tasks.processResources

    // --- Collect versioned Paper/Spigot modules ---
    def versionedProjects = rootProject.subprojects.findAll {
        it.path.startsWith(":paper:") || it.path.startsWith(":spigot:")
    }.findAll { it.name ==~ /v\d+_\d+_R\d+/ }

    versionedProjects.each { proj ->
        if (proj.tasks.findByName("jar") != null) {
            dependsOn proj.tasks.named("jar")
        }
    }

    doFirst {
        logger.lifecycle("Building versioned NMS subprojects before shading: ${versionedProjects*.path}")
        logger.lifecycle("Running ShadowJar with isolated classpath...")
    }

    // --- Dependency and exclude cleanup ---
    dependencies {
        exclude(dependency("com.google.guava:guava"))
    }

    // --- Exclude embedded JARs and redundant libs ---
    exclude '**/*.jar'
    exclude 'com/google/common/**'
    exclude 'com/google/thirdparty/**'

    // --- Normal shadow output config ---
    destinationDirectory.set(file("${rootDir}/target"))
    archiveBaseName.set("Hellblock")
    entryCompression = ZipEntryCompression.DEFLATED
    archiveVersion.set(hellblockLibs.versions.pluginVersion.get())
    archiveClassifier.set('')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    mergeServiceFiles()
    zip64 = true

    minimize {
        exclude(project(":api"))
    }

    // --- Relocations ---
    relocate 'com.mysql', 'com.swiftlicious.hellblock.libraries.mysql'
    relocate 'org.mariadb', 'com.swiftlicious.hellblock.libraries.mariadb'
    relocate 'org.postgresql', 'com.swiftlicious.hellblock.libraries.postgresql'
    relocate 'com.zaxxer.hikari', 'com.swiftlicious.hellblock.libraries.hikari'
    relocate 'redis.clients.jedis', 'com.swiftlicious.hellblock.libraries.jedis'
    relocate 'com.mongodb', 'com.swiftlicious.hellblock.libraries.mongodb'
    relocate 'org.bson', 'com.swiftlicious.hellblock.libraries.bson'
    relocate 'org.apache.commons.pool2', 'com.swiftlicious.hellblock.libraries.commonspool2'
    relocate 'net.objecthunter.exp4j', 'com.swiftlicious.hellblock.libraries.exp4j'
    relocate 'net.jpountz', 'com.swiftlicious.hellblock.libraries.jpountz'
    relocate 'net.kyori', 'com.swiftlicious.hellblock.libraries'
    relocate 'com.github.benmanes.caffeine', 'com.swiftlicious.hellblock.libraries.caffeine'
    relocate 'org.incendo', 'com.swiftlicious.hellblock.libraries'
    relocate 'dev.dejvokep.boostedyaml', 'com.swiftlicious.hellblock.libraries.boostedyaml'
    relocate 'net.bytebuddy', 'com.swiftlicious.hellblock.libraries.bytebuddy'
    relocate 'com.saicone.rtag', 'com.swiftlicious.hellblock.libraries.rtag'
    relocate 'io.papermc.lib', 'com.swiftlicious.hellblock.libraries.paperlib'
    relocate 'com.mojang.authlib', 'com.swiftlicious.hellblock.libraries.authlib'
}

artifacts {
    add("default", tasks.shadowJar)
}

tasks.build {
    dependsOn tasks.shadowJar
}

tasks.jar {
    enabled = false
}

tasks.named("processResources") {
    outputs.upToDateWhen { false } // Always re-run, even if inputs look up-to-date
}

tasks.named("shadowJar") {
    outputs.upToDateWhen { false } // Always rebuild final JAR
}