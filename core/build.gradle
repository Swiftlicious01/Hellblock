// --- ensure Shadow uses its own ASM version, not Paperweight’s ---
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.ow2.asm:asm:9.7'
        classpath 'org.ow2.asm:asm-commons:9.7'
        classpath 'org.ow2.asm:asm-tree:9.7'
        classpath 'org.ow2.asm:asm-util:9.7'
    }
}

// --- now declare plugins (Gradle 9+ requires this order) ---
plugins {
    id 'com.gradleup.shadow' version '9.0.0-beta11'
    id 'java'
}

description = "Core for Hellblock"

repositories {
    maven {
        url = 'https://maven.enginehub.org/repo/'
        content {
            includeGroup 'com.sk89q.worldedit'
            includeGroup 'com.sk89q.worldguard'
            includeGroup 'com.sk89q.worldedit.worldedit-libs'
            includeGroup 'com.sk89q.worldguard.worldguard-libs'
        }
    }
    maven { url = 'https://jitpack.io/' }
    maven { url = 'https://libraries.minecraft.net' }
    maven {
        url = 'https://repo.papermc.io/repository/maven-public'
        content { includeGroup 'dev.folia' }
        metadataSources { mavenPom(); artifact() }
    }
    maven { url = 'https://mvn.lumine.io/repository/maven-public/' }
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://repo.glaremasters.me/repository/concuncan/' }
    maven { url = 'https://repo.bg-software.com/repository/api/' }
    maven { url = 'https://nexus.neetgames.com/repository/maven-public' }
    maven { url = 'https://repo.codemc.io/repository/maven-public/' }
    maven { url = 'https://repo.infernalsuite.com/repository/maven-snapshots/' }
    maven { url = 'https://repo.rapture.pw/repository/maven-releases/' }
    maven { url = 'https://repo.opencollab.dev/main/' }
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
        content { includeGroup 'me.clip' }
    }
}

dependencies {
    // Paper APIs (or spigot for testing purposes only, will always depend on paper)
    compileOnly hellblockLibs.folia
    // compileOnly hellblockLibs.spigot
    compileOnly hellblockLibs.aswm

    // Internal API
    implementation project(":api")

    // --- ASM FIX for Shadow 9 ---
    implementation 'org.ow2.asm:asm:9.7'
    implementation 'org.ow2.asm:asm-commons:9.7'
    implementation 'org.ow2.asm:asm-tree:9.7'
    implementation 'org.ow2.asm:asm-util:9.7'

	// --- Auto-discover and add versioned Paper & Spigot modules safely ---

	// PAPER (Mojang-mapped) IMPLS — default configuration
	rootProject.childProjects['paper']?.childProjects
    	?.findAll { it.key ==~ /v\d+_\d+_R\d+/ } // Only versioned modules
    	?.each { name, proj ->
        	if (proj.configurations.findByName("reobf") != null) {
            	// If somehow a Paper module has reobf (rare), use it
            	implementation project(path: proj.path, configuration: "reobf")
        	} else {
            	implementation project(proj.path)
        	}
    	}

	// SPIGOT (Spigot-mapped) IMPLS — depend on reobf output if available
	rootProject.childProjects['spigot']?.childProjects
    	?.findAll { it.key ==~ /v\d+_\d+_R\d+/ } // Only versioned modules
    	?.each { name, proj ->
        	if (proj.configurations.findByName("reobf") != null) {
           	 	implementation project(path: proj.path, configuration: "reobf")
        	} else {
            	implementation project(proj.path)
        	}
    	}

    implementation hellblockLibs.rtagcore
    implementation hellblockLibs.rtagitem
    implementation hellblockLibs.rtagentity
    implementation hellblockLibs.rtagblock

    implementation(hellblockLibs.adventureapi) {
        exclude module: 'adventure-bom'
        exclude module: 'checker-qual'
        exclude module: 'annotations'
    }
    implementation hellblockLibs.adventureminimessage
    implementation hellblockLibs.adventureplatform
    implementation hellblockLibs.adventurejsonlegacy
    implementation hellblockLibs.adventureplain
    implementation hellblockLibs.adventurenbt
    implementation(hellblockLibs.adventuregson) {
        exclude group: 'com.google.code.gson'
    }

    implementation hellblockLibs.paperlib
    implementation hellblockLibs.authlib
    compileOnly hellblockLibs.jackson
    compileOnly hellblockLibs.jacksonannotations

    // Cloud & optional APIs
    compileOnly hellblockLibs.cloudcore
    compileOnly hellblockLibs.cloudminecraftextras
    compileOnly hellblockLibs.cloudpaper
    compileOnly hellblockLibs.immutables
    compileOnly hellblockLibs.placeholderapi
    compileOnly hellblockLibs.gson
    compileOnly hellblockLibs.log4j
    compileOnly hellblockLibs.slf4j

    // WorldEdit / WorldGuard
    compileOnly hellblockLibs.worldeditcore
    compileOnly hellblockLibs.worldeditbukkit
    compileOnly hellblockLibs.worldeditlibscore
    compileOnly hellblockLibs.worldeditlibsbukkit
    compileOnly hellblockLibs.worldguardcore
    compileOnly hellblockLibs.worldguardbukkit
    compileOnly hellblockLibs.worldguardlibscore

    // Database & caching
    compileOnly hellblockLibs.sqlite
    compileOnly hellblockLibs.h2
    compileOnly hellblockLibs.mongodb
    compileOnly hellblockLibs.hikari
    compileOnly hellblockLibs.jedis
    compileOnly hellblockLibs.caffeine
    compileOnly hellblockLibs.exp4j
    compileOnly hellblockLibs.lz4
    compileOnly hellblockLibs.boostedyaml

    // Geyser
    compileOnly hellblockLibs.geyser
    compileOnly hellblockLibs.floodgate

    // External plugin APIs
    compileOnly(hellblockLibs.vault) { exclude group: 'org.bukkit' }
    compileOnly hellblockLibs.mythicmobs
    compileOnly(hellblockLibs.shopgui) { exclude group: 'org.spigotmc', module: 'spigot-api' }
    compileOnly hellblockLibs.jobs
    compileOnly hellblockLibs.wildstacker
    compileOnly hellblockLibs.mcmmo
    compileOnly hellblockLibs.stackermob

    // Local libs
    compileOnly files("libs/AdvancedEnchantments-api.jar")
}

compileJava {
    options.compilerArgs += ['-Xlint:all', '-Xlint:-processing']
    options.encoding = 'UTF-8'
}

java {
    toolchain { languageVersion = JavaLanguageVersion.of(21) }
}

configurations.all {
    resolutionStrategy.force 'org.ow2.asm:asm:9.7'
    resolutionStrategy.force 'org.ow2.asm:asm-commons:9.7'
    resolutionStrategy.force 'org.ow2.asm:asm-tree:9.7'
    resolutionStrategy.force 'org.ow2.asm:asm-util:9.7'
}

tasks.named("shadowJar") { task ->

    // Precompute the filtered files provider at configuration time
    def filteredFilesProvider = project.provider {
        def result = []
        if (task.hasProperty("configurations")) {
            def configs = task.configurations.getOrElse([])
            configs.each { cfg ->
                cfg.resolve().each { file ->
                    def name = file.name.toLowerCase()
                    if (!name.contains("paperweight") && !name.contains("dev-bundle")) {
                        result << file
                    }
                }
            }
        }
        // Build a detached FileCollection here, so no project.files inside doFirst
        return project.layout.files(result)
    }

    doFirst {
        // Only read the provider here — no project calls
        def detachedFiles = filteredFilesProvider.get()
        if (!detachedFiles.isEmpty()) {
            task.from(detachedFiles)
        }
        logger.lifecycle("Running ShadowJar with isolated classpath...")
        logger.lifecycle("Filtered ${detachedFiles.files.size()} JARs for shading.")
    }

    // Normal Shadow config
    destinationDirectory.set(file("${rootDir}/target"))
    archiveBaseName.set("Hellblock")
    archiveVersion.set(hellblockLibs.versions.pluginVersion.get())
    archiveClassifier.set('')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    mergeServiceFiles()

    // Relocations at config time (safe)
    relocate 'com.mysql', 'com.swiftlicious.hellblock.libraries.mysql'
    relocate 'org.mariadb', 'com.swiftlicious.hellblock.libraries.mariadb'
    relocate 'org.postgresql', 'com.swiftlicious.hellblock.libraries.postgresql'
    relocate 'com.zaxxer.hikari', 'com.swiftlicious.hellblock.libraries.hikari'
    relocate 'redis.clients.jedis', 'com.swiftlicious.hellblock.libraries.jedis'
    relocate 'com.mongodb', 'com.swiftlicious.hellblock.libraries.mongodb'
    relocate 'org.bson', 'com.swiftlicious.hellblock.libraries.bson'
    relocate 'org.apache.commons.pool2', 'com.swiftlicious.hellblock.libraries.commonspool2'
    relocate 'net.objecthunter.exp4j', 'com.swiftlicious.hellblock.libraries.exp4j'
    relocate 'net.jpountz', 'com.swiftlicious.hellblock.libraries.jpountz'
    relocate 'net.kyori', 'com.swiftlicious.hellblock.libraries'
    relocate 'com.github.benmanes.caffeine', 'com.swiftlicious.hellblock.libraries.caffeine'
    relocate 'org.incendo', 'com.swiftlicious.hellblock.libraries'
    relocate 'dev.dejvokep.boostedyaml', 'com.swiftlicious.hellblock.libraries.boostedyaml'
    relocate 'com.saicone.rtag', 'com.swiftlicious.hellblock.libraries.rtag'
    relocate 'io.papermc.lib', 'com.swiftlicious.hellblock.libraries.paperlib'
    relocate 'com.mojang.authlib', 'com.swiftlicious.hellblock.libraries.authlib'
}

artifacts {
    add("default", tasks.shadowJar)
}

tasks.build {
    dependsOn tasks.shadowJar
}

tasks.jar {
    enabled = false
}